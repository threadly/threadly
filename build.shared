apply plugin: 'java'
apply plugin: 'maven'

repositories {
  mavenCentral()
}

dependencies {
  testCompile group: 'junit', name: 'junit', version: '4.12'
}

// Set compatibility to 1.8 so jdk 9+ can be used to compile this project with binary compatibility Java 8
tasks.withType(JavaCompile) {
  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'
}

compileJava {
  options.compilerArgs << "-Xlint:all" << "-Xlint:-deprecation" << "-Werror"

  if (JavaVersion.current().isJava8()) {
    options.compilerArgs << "-XDenableSunApiLintControl" << "-Xlint:-sunapi"
  }
}

compileTestJava {
  options.compilerArgs << "-Xlint:all"
}

test {
  getReports().getJunitXml().setDestination(file("$buildDir/reports/tests/xml"))
  getReports().getHtml().setDestination(file("$buildDir/reports/tests/html"))
  setBinResultsDir(file("$buildDir/reports/tests/bin"))
}

jar {
  manifest {
    attributes 'Implementation-Title': 'Threadly', 'Implementation-Version': version
  }
}

javadoc {
  source = sourceSets.main.allJava
  excludes = ['**/ThreadlyInternalAccessor**', '**/ArgumentVerifier**']
  options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PUBLIC
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}
