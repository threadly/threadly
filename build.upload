apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'checkstyle'

group 'org.threadly'
version = '0.7-SNAPSHOT'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
  mavenCentral()
}

dependencies {
  testCompile group: 'junit', name: 'junit', version: '4.+'
}

tasks.withType(Compile) {
    options.compilerArgs << "-Xlint:-options"
} 

plugins.withType(JavaPlugin) {
  checkstyle.sourceSets = [sourceSets.main]
}

test {
  testResultsDir = file("$buildDir/test-results/xml")
}

jar {
  manifest {
    attributes 'Implementation-Title': 'Threadly', 'Implementation-Version': version
  }
}

javadoc {
  source = sourceSets.main.allJava
  options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PUBLIC
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment {
        MavenDeployment deployment -> signing.signPom(deployment)
      }
 
      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }
      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
      }
 
      pom.project {
        name 'Threadly'
        packaging 'jar'
        description 'A library of tools to assist with concurrent java development, with a large focus on testability.'
        url 'http://threadly.org/'
 
        scm {
          url 'scm:git@github.com:jentfoo/threadly.git'
          connection 'scm:git@github.com:jentfoo/threadly.git'
          developerConnection 'scm:git@github.com:jentfoo/threadly.git'
        }
 
        licenses {
          license {
            name 'GNU LESSER GENERAL PUBLIC LICENSE (LGPLv3)'
            url 'http://www.gnu.org/licenses/lgpl-3.0.txt'
            distribution 'repo'
          }
        }
 
        developers {
          developer {
            id 'jent'
            name 'Mike Jensen'
          }
        }
      }
    }
  }
}
